# Maintainer: Terje Larsen <terlar@gmail.com>

_name=x-on-resize
pkgname=x-on-resize-git
pkgver=0.2.3.g617e5ed
pkgrel=1

pkgdesc='A simple display configuration daemon'
arch=('i686' 'x86_64')
url='http://keithp.com/blogs/x-on-resize'
license=('GPL2')

depends=('xorg-xrandr')
makedepends=('git' 'xorg-util-macros')

conflicts=("${_name}")
provides=("${_name}")
source=("git+git://people.freedesktop.org/~keithp/${_name}")
sha1sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_name}"
  git describe | sed -e 's/v//' -e 's/-/./g'
}

build() {
  cd "${srcdir}/${_name}"
  ./autogen.sh --prefix /usr
  make
}

package() {
  cd "${srcdir}/${_name}"
  make install DESTDIR="${pkgdir}"
}
